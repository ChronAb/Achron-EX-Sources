// Copyright (C) 2002-2011 Hazardous Software Inc.  See EULA for license information.

#include common/include/unit_common_header.aih

//pod* class progen units idle mode, look for other progenitors to set the cap bits
//allowing themselves to produce correct units
//store the two best 'mates' into AdditionalParameters, most sig 16 bits and least sig 16 bits.
//and check if those mates are still valid every other tick
//then, twice per second, check to see if there are other progen units nearby that are 'better', 
//eg, prioritize mates that already have another mate next to them to complete a triad

//Also performs Smart Production functions

PERFORM GET_CUR_TIME;
int curtime = perf_ret;
int curage = unit->Age;
int apidle = unit->AdditionalParameters3;
int uclass = unit->Class;
query_vis_distance = -1;

/*Smart Production:
	All units complete auto-production within a 90 tick (5s) window, occuring every minute. Timing in the window is based on unit number so resources can be re-evaluated	in between.
*/
int period = 1620; //pod class auto-production period is 90 s
int offset = 0;
if(uclass == $OCTOPOD_FACTORY_CLASS){
	offset = 540; //Octo-progen pod auto-production is offset by 30 s
}
if(uclass == $PHAROPOD_FACTORY_CLASS){
	offset = 1080; //Pharo-progen pod auto-production is offset by 60 s
}

/*if(apidle >= 4){//Special extra conditions for ligo class auto-production
	period = 3240; //ligo class period is 180 s (3 minutes)
	if(uclass == $SEPIPPOD_FACTORY_CLASS){
		offset = 2160; //Sepi-progen ligo auto-production is offset by 120 s
	}
}*/

if(curtime%period==unit%90+offset){//Is this tick my turn?
	if (apidle!=0 && unit->Armour==0 && player->$LCrystals>=$LC_SMART_THRESHOLD
		&& player->$QPlasma>=$QP_SMART_THRESHOLD && player->$Power > $POWER_SMART_THRESHOLD) {//are the smart-production conditions met?
		
		int oprogen = apidle + 37;
		PERFORM SET_OBJECTIVE oprogen;//try to make the unit
		PERFORM NOTHING;
	}
}


//OBJECTIVE NUMBER:
//If orders are queued and objective != 0 then change to 0
//Otherwise, every 6 ticks:
//if idle objective number does not match AP3 value from last unit produced
//then change to match
if(unit->NumQueuedObjectives > 0){
	if(unit->Objective != 0) {	PERFORM SET_OBJECTIVE 0; }
}
else{
	if (curtime%6==0) {
		int oidle = 0;
		if(apidle >= 1) oidle = apidle + 21;
		if( unit->Objective != oidle) {	PERFORM SET_OBJECTIVE oidle; }
	}
}


if (curtime[0] == 0) {
	int AP = unit->AdditionalParameters;
	int status = unit->Status;
	int update = 0;
	int aclass= 0;
	
	if(player->Advancements[$GREKIM_ADV_VANGUARD]){
		if (!status[$CAP_BIT_2] || !status[$CAP_BIT_3]){
			PERFORM $ACTION_A;
			PERFORM $ACTION_B;
			PERFORM $ACTION_C;
		}
	}
	else{
    
		object A = AP[0,16];
		int alive = A->IsAlive;
		if (alive==1) { aclass = A->Class; }
		if (status[$CAP_BIT_2] && (alive!=1 || A<_>unit > unit.SightHRange2 || (aclass!=$OCTOPOD_FACTORY_CLASS && aclass!=$SEPIPPOD_FACTORY_CLASS && aclass!=$PHAROPOD_FACTORY_CLASS ) ) ) {  
				PERFORM $ACTION_CLEAR_A; 
				AP[0,16] = 0;
				update = 1;
		}
		
		object B = AP[$STOREDUNIT];
		alive = B->IsAlive;
		if (alive==1) { aclass = B->Class; }
		if (status[$CAP_BIT_3] && (alive!=1 || B<_>unit > unit.SightHRange2 || (aclass!=$OCTOPOD_FACTORY_CLASS && aclass!=$SEPIPPOD_FACTORY_CLASS && aclass!=$PHAROPOD_FACTORY_CLASS ) ) ) {  
				PERFORM $ACTION_CLEAR_B; 
				AP[$STOREDUNIT] = 0;
				update = 1;
		}

		if (update) { PERFORM SET_ADDITIONAL_PARAMS AP; }
		else {
			if (status[$CAP_BIT_4]) {
				if (A<_>B > unit.SightHRange2) {
					PERFORM $ACTION_CLEAR_C;
				}
			}
		}
		
	}
}

object commander = unit->Commander;
if ( commander>0 && commander.Rank != $ARCTICUS_RANK) {
    PERFORM $ACTION_CHANGE_COMMANDER 0; 
}

//if (unit->Objective!=0) { 	PERFORM SET_OBJECTIVE 0; }

if (curtime%87==0) {
	PERFORM CLEAR_LAST_ATTACKED_DIRECTION;
}

if (curtime % 9 = 0 && !player->Advancements[$GREKIM_ADV_VANGUARD] ) { //only run this every 2/sec

	int ap = unit->AdditionalParameters;
	int myStatus = unit->Status;


	int b = $SEPIPPOD_FACTORY_CLASS;
	int a = $PHAROPOD_FACTORY_CLASS;
	if (uclass==$SEPIPPOD_FACTORY_CLASS) {
			b = $OCTOPOD_FACTORY_CLASS;
			a = $PHAROPOD_FACTORY_CLASS;
	} 
	if (uclass==$PHAROPOD_FACTORY_CLASS) {
			b = $OCTOPOD_FACTORY_CLASS;
			a = $SEPIPPOD_FACTORY_CLASS; 
	}

	//prioritize mates that already have a third triad buddy connected to them (capbits 2 and 3 set)
	int apupdate = 0;
	int qs = 0;
	object mateA = QUERY UNIT [unit] MAX [(qs=query->Status)[$CAP_BIT_2] + qs[$CAP_BIT_3] ]  WHERE [ query->Class==a  && query->Energy > 1 && query->Owner==player];
	if (mateA->IsAlive==1 && mateA<_>unit <= unit.SightHRange2 && ap[0,16]!=mateA) {
			ap[0,16] = mateA;
			PERFORM $ACTION_A;
			apupdate = 1;
	}

	object mateB = QUERY UNIT [unit] MAX [(qs=query->Status)[$CAP_BIT_2] + qs[$CAP_BIT_3] ]  WHERE [ query->Class==b && query->Energy > 1  && query->Owner==player];
	if (mateB->IsAlive==1 && mateB<_>unit <= unit.SightHRange2 && ap[$STOREDUNIT]!=mateB) {
			ap[$STOREDUNIT] = mateB;
			PERFORM $ACTION_B;
			apupdate = 1;
	}

	mateA = ap[0,16];
	mateB = ap[$STOREDUNIT];
	if (mateA>0 && mateB>0 && mateA<_>mateB <= unit.SightHRange2) {
		PERFORM $ACTION_C;
	}

	if (apupdate) { PERFORM SET_ADDITIONAL_PARAMS ap; }
}

//attack nearby targets, the attack action can't be run too often as configured in the XML, so the script will only check for enemies every 4 seconds
if (curage%72==0) {
	//find closest enemy un-tss'd unit...
	query_vis_distance = 20; //weapon range of all *POD CLASS UNITS
	int qmaxHP = 0;
	target = QUERY UNIT [unit] MIN [ |/(unit <=> query) * query->HP / log(qmaxHP) ] 
						   WHERE [!(query->IsAlly) && query.Rank>=$TURRET_RANK && (query->IsStasis!=1) && (qmaxHP=query.MaxHP)>0];  //don't look for buildings except turrets (rank>=90)
	if (target < 1) {                   
		//look for any building
		target = QUERY UNIT [unit] MIN [ |/(unit <=> query) * query->HP / log(qmaxHP) ] WHERE [!(query->IsAlly) && (query->IsStasis!=1) && (qmaxHP=query.MaxHP)>0]; 
		if (target < 1) {   
			PERFORM NOTHING;
		}        
	}
	
	int targetOnGround = 1;			
	targetOnGround = target->IsTerrainGround;
	int position = -1;
	if (targetOnGround) { 
		PERFORM GET_CLOSEST_TARGET_POSITION_WITHIN_RANGE $ACTION_ATTACK;
	} 
	else {
		PERFORM GET_CLOSEST_TARGET_POSITION_WITHIN_RANGE $ACTION_ATTACK_W1;
	}
	position = perf_ret;
	if (position!=$INVALID_POSITION) { 
		target = position;
		if(targetOnGround) {
			PERFORM IS_POSITION_WITHIN_ACTION_ANGLE $ACTION_ATTACK;
		}
		else {
			PERFORM IS_POSITION_WITHIN_ACTION_ANGLE $ACTION_ATTACK_W1;
		}
		if (perf_ret>0) { //target is attackable in this direction
				
			target_is_position = 1;
			target = position;
			if (targetOnGround) { PERFORM $ACTION_ATTACK position; }
			else { PERFORM $ACTION_ATTACK_W1 position; }
			PERFORM NOTHING;
		}
	}
}
